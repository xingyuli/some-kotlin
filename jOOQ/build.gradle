import groovy.xml.MarkupBuilder
import org.jooq.util.GenerationTool

import javax.xml.bind.JAXB

dependencies {
    compile "org.jooq:jooq:$jooqVersion"

    runtime "mysql:mysql-connector-java:$mysqlConnectorVersion"
}

task jooqCodegen {
    doLast {
        // Use your favourite XML builder to construct the code generation configuration file
        // ----------------------------------------------------------------------------------
        def writer = new StringWriter()
        def xml = new MarkupBuilder(writer)
                .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.7.0.xsd') {
            jdbc() {
                driver('com.mysql.jdbc.Driver')
                url('jdbc:mysql://localhost:3306/diandu_cms?useUnicode=true&characterEncoding=utf-8')
                user('root')
                password('root')
            }
            generator() {
                database() {
                    inputSchema('diandu_cms')
                }
                generate() {
                    daos("true")
                }
                target() {
                    packageName('org.swordess.somekotlin.jooq.db')
                    directory('src/gen/java')
                }
            }
        }

        // Run the code generator
        // ----------------------
        GenerationTool.generate(
                JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
        )
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'src/gen/java'
        }
    }
}
